unit TestAutoMapper;
{

  Delphi DUnit Test Case
  ----------------------
  This unit contains a skeleton test case class generated by the Test Case Wizard.
  Modify the generated code to correctly setup and call the methods from the unit 
  being tested.

}

interface

uses
  TestFramework,
  Test.Models,
  Test.ModelsDTO,
  AutoMapper.Mapper,
  AutoMapper.ConfigurationProvider,
  Spring,
  Spring.Collections,
  AutoMapper.ClassPair
  ;

type

  TestTMapper = class(TTestCase)
  strict private
    const
      CS_LAST_NAME          = 'Иванов';
      CS_FIRST_NAME         = 'Сергей';
      CS_MIDDLE_NAME        = 'Николаеивч';
      CS_AGE                =  26;
      CS_STREET             = 'Гагарина';
      CS_NUM_HOUSE          =  34;
      CS_FULL_NAME          = CS_LAST_NAME+' '+CS_FIRST_NAME+' '+CS_MIDDLE_NAME+', возраст: 26';
      CS_FULL_ADDRESS       = 'ул. '+CS_STREET+' д. 34';
      CS_OVERRIDE_FULL_NAME = 'Петрович Петр Игнатьевич';
      СS_OVERRIDE_ADDRESS   = 'ул. Некрасова';
      CS_POST               = 'Генеральный менеджер по клинигу';
      CS_STATUS             = 12;
  strict private
    FAddress    : TAddress;
    FPerson     : TPerson;

    FMapper: TMapper;
    procedure _Configure;
  private
    procedure CreateAddressDTO(var FAddressDTO: TAddressDTO);
    procedure CreatePersonDTO(FAddressDTO: TAddressDTO; var FPersonDTO: TPersonDTO);
  public
    procedure SetUp; override;
    procedure TearDown; override;
  published
    procedure TestConfigure;
    procedure TestMap_AutoConfig;
    procedure TestMap_CustomConfig;
    procedure TestMap_WithConstructor;
    procedure TestMap_WithNestedObject;
    procedure TestMap_WithConstructorAndNestedObject;
    procedure TestMap_WithOverrideExpression;
  end;

implementation
uses
  System.SysUtils;

procedure TestTMapper.SetUp;
begin
  FAddress := TAddress.Create(CS_STREET, CS_NUM_HOUSE);
  FPerson  := TPerson.Create(CS_LAST_NAME, CS_FIRST_NAME, CS_MIDDLE_NAME, CS_AGE, FAddress, CS_POST);
  FPerson.Status := CS_STATUS;

  FMapper := Mapper;
end;

procedure TestTMapper.TearDown;
begin
//  FMapper.Free;
  FPerson.Free;
  FAddress.Free;
end;
//---
procedure TestTMapper.TestConfigure;
begin
  _configure;
end;

procedure TestTMapper._Configure;
begin
  Mapper.Reset;
  Mapper.Configure(procedure (const cfg: TConfigurationProvider)
                  begin
                    cfg.CreateMap<TPerson, TUserDTO>(procedure (const FPerson: TPerson; out FUserDTO: TUserDTO)
                                                        begin
                                                          FUserDTO.FullName := FPerson.LastName    +' '+
                                                                               FPerson.FirstName   +' '+
                                                                               FPerson.MiddleName  +', возраст: '+
                                                                               FPerson.Age.ToString;
                                                          FUserDTO.Address  := 'ул. '+ FPerson.Address.Street
                                                                              +' д. '+ FPerson.Address.NumHouse.ToString;
                                                        end
                                                      )
                       .CreateMap<TAddress, TAddressDTO>
                       .CreateMap<TAddressDTO, TAddress>(procedure (const FAddressDTO: TAddressDTO; out FAddress: TAddress)
                                                          begin
                                                            FAddress := TAddress.Create(FAddressDTO.Street,
                                                                                        FAddressDTO.NumHouse);
                                                          end
                                                        )
                       .CreateMap<TPersonDTO, TPerson>(procedure (const FPersonDTO: TPersonDTO; out FPerson: TPerson)
                                                        begin
                                                          FPerson := TPerson.Create(FPersonDTO.LastName,
                                                                                    FPersonDTO.FirstName,
                                                                                    FPersonDTO.MiddleName,
                                                                                    FPersonDTO.Age,
                                                                                    mapper.Map<TAddress>(FPersonDTO.Address),
                                                                                    FPersonDTO.Post);
                                                        end
                                                      )
                       .CreateMap<TPerson, TPersonDTO>(procedure (const s: TPerson; out d: TPersonDTO)
                                                        begin
                                                          d.LastName    := s.LastName;
                                                          d.FirstName   := s.FirstName;
                                                          d.MiddleName  := s.MiddleName;
                                                          d.Age         := s.Age;
                                                          d.Address     := mapper.Map<TAddressDTO>(s.Address);
                                                          d.Post        := s.Post;
                                                        end
                                                       )
                       .CreateMap<TPerson, TSimplePersonDTO>();

                  end);
end;

procedure TestTMapper.TestMap_AutoConfig;
var
  FSimplePersonDTO: TSimplePersonDTO;
begin
  _configure;

  FSimplePersonDTO := mapper.Map<TSimplePersonDTO>(FPerson);

  CheckEquals(CS_LAST_NAME,   FSimplePersonDTO.Last_Name);
  CheckEquals(CS_FIRST_NAME,  FSimplePersonDTO.First_Name);
  CheckEquals(CS_POST,        FSimplePersonDTO.Post);
  CheckEquals(CS_STATUS,      FSimplePersonDTO.Status.Value);
  { TODO : Добавить возможность устанавливать кастомное приведение типов. }
  CheckEquals(CS_MIDDLE_NAME, FSimplePersonDTO.Middle_Name, 'Так как в объекте ресурса MiddleName: Nullable<string>, а в объекте назначения Middle_Mame: string - автоматическое приведение типов стандртными средствами не возможно.');
end;

procedure TestTMapper.TestMap_CustomConfig;
var
  FUserDTO: TUserDTO;
begin
  _configure;

  FUserDTO := mapper.Map<TUserDTO>(FPerson);

  CheckEquals(CS_FULL_NAME,    FUserDTO.FullName);
  CheckEquals(CS_FULL_ADDRESS, FUserDTO.Address);
end;

procedure TestTMapper.TestMap_WithConstructor;
 var
  FAddress:   TAddress;
  FAddressDTO: TAddressDTO;
begin
  _configure;
  CreateAddressDTO(FAddressDTO);

  FAddress := mapper.Map<TAddress>(FAddressDTO);

  CheckEquals(CS_STREET,    FAddress.Street);
  CheckEquals(CS_NUM_HOUSE, FAddress.NumHouse);
end;

procedure TestTMapper.TestMap_WithNestedObject;
var
  FPersonDTO: TPersonDTO;
begin
  _configure;

  FPersonDTO := mapper.Map<TPersonDTO>(FPerson);

  CheckEquals(CS_LAST_NAME,    FPersonDTO.LastName);
  CheckEquals(CS_FIRST_NAME,   FPersonDTO.FirstName);
  CheckEquals(CS_MIDDLE_NAME,  FPersonDTO.MiddleName.Value);
  CheckEquals(CS_AGE,          FPersonDTO.Age.Value);
  CheckEquals(CS_STREET,       FPersonDTO.Address.Street);
  CheckEquals(CS_NUM_HOUSE,    FPersonDTO.Address.NumHouse);
end;

procedure TestTMapper.TestMap_WithConstructorAndNestedObject;
var
  FPerson: TPerson;
  FPersonDTO: TPersonDTO;
  FAddressDTO: TAddressDTO;
begin
  _configure;

  CreateAddressDTO(FAddressDTO);
  CreatePersonDTO(FAddressDTO, FPersonDTO);

  FPerson := mapper.Map<TPerson>(FPersonDTO);

  CheckEquals(CS_LAST_NAME,    FPerson.LastName);
  CheckEquals(CS_FIRST_NAME,   FPerson.FirstName);
  CheckEquals(CS_MIDDLE_NAME,  FPerson.MiddleName.Value);
  CheckEquals(CS_AGE,          FPerson.Age.Value);
  CheckEquals(CS_STREET,       FPerson.Address.Street);
  CheckEquals(CS_NUM_HOUSE,    FPerson.Address.NumHouse);
end;

procedure TestTMapper.TestMap_WithOverrideExpression;
var
  FUserDTO: TUserDTO;
begin
  _configure;

  FUserDTO := mapper.Map<TPerson, TUserDTO>(FPerson, procedure (const s: TPerson; out d:  TUserDTO)
                                              begin
                                                d.FullName := CS_OVERRIDE_FULL_NAME;
                                                d.Address  := СS_OVERRIDE_ADDRESS;
                                              end
                                            );

  CheckEquals(CS_OVERRIDE_FULL_NAME, FUserDTO.FullName);
  CheckEquals(СS_OVERRIDE_ADDRESS,   FUserDTO.Address);
end;

procedure TestTMapper.CreatePersonDTO(FAddressDTO: TAddressDTO; var FPersonDTO: TPersonDTO);
begin
  FPersonDTO := TPersonDTO.Create;
  FPersonDTO.LastName   := CS_LAST_NAME;
  FPersonDTO.FirstName  := CS_FIRST_NAME;
  FPersonDTO.MiddleName := CS_MIDDLE_NAME;
  FPersonDTO.Age        := CS_AGE;
  FPersonDTO.Address    := FAddressDTO;
end;


procedure TestTMapper.CreateAddressDTO(var FAddressDTO: TAddressDTO);
begin
  FAddressDTO := TAddressDTO.Create;
  FAddressDTO.Street   := CS_STREET;
  FAddressDTO.NumHouse := CS_NUM_HOUSE;
end;

initialization
  // Register any test cases with the test runner
  RegisterTest(TestTMapper.Suite);
end.

